# First we configure a SCEP issuer for the Okta CA,
# with dynamic SCEP challenges

resource "zentral_mdm_scep_issuer" "okta" {
  name        = "Okta"
  description = "Okta Device Trust CA"

  url       = "https://example.okta.com/pki/000/scep/aaa"
  key_size  = 2048
  key_usage = 1

  backend = "OKTA_CA"
  okta_ca = {
    url      = "https://example.okta.com/api/v1/certificateAuthorities/000/registrationAuthorities/aaa/challenge"
    username = "okta-AAAAAA"
    password = var.okta_challenge_password
  }
}

# Then we configure a Certificate Asset.

resource "zentral_mdm_artifact" "okta-cert" {
  name               = "Okta Certificate"
  type               = "Certificate Asset"
  channel            = "Device"
  platforms          = ["iOS", "iPadOS", "macOS"]
  reinstall_interval = 180 # renew every 180 days
}

# A Certificate Asset artifact is configured with `zentral_mdm_cert_asset` resources.
# ACME and SCEP issuers can be used. Zentral will automatically return
# a `com.apple.asset.credential.acme` or `com.apple.asset.credential.scep`
# depending on the capabilities of the devices and the issuers configuration.

resource "zentral_mdm_cert_asset" "okta-cert-v1" {
  artifact_id = zentral_mdm_artifact.okta-cert.id
  # We configure only a SCEP issuer because Okta doesn't support ACME (yet?).
  scep_issuer_id = zentral_mdm_scep_issuer.okta.id

  # the variables will be subsituted
  subject = [
    {
      type  = "CN",
      value = "$REALM_USER.EMAIL managementAttestation $ENROLLED_DEVICE.SERIAL_NUMBER"
    }
  ]
  ios     = true
  ipados  = true
  macos   = true
  version = 1
}

# We can use the certificate in other declarations, like for
# a `com.apple.configuration.security.identity` for example.

resource "zentral_mdm_artifact" "okta-identity" {
  name      = "Okta Identity"
  type      = "Configuration"
  channel   = "Device"
  platforms = ["iOS", "iPadOS", "macOS"]
}

resource "zentral_mdm_declaration" "okta-identity-v1" {
  artifact_id = zentral_mdm_artifact.okta-identity.id
  source = jsonencode({
    Type        = "com.apple.configuration.security.identity",
    Identifier  = "com.example.okta-identity",
    ServerToken = "8acee36b-bee2-4dd1-972d-a7c8eee36ea5",
    Payload = {
      KeyIsExtractable   = false,
      AllowAllAppsAccess = true,
      # This is substituted with the `Identifier` of the 
      # `com.apple.asset.credential.acme` or `com.apple.asset.credential.scep`
      # generated by the Cert Asset for the device.
      CredentialAssetReference = "ztl:${zentral_mdm_artifact.okta-cert.id}"
    }
  })
  ios     = true
  ipados  = true
  macos   = true
  version = 1
}
